# Maintainer: Huang-Huang Bao <eh5@sokka.cn>
# Contributor: Pellegrino Prevete <pellegrinoprevete@gmail.com>
# Contributor: dexterlb <dexterlb@qtrp.org>

_pkgname=openfec
pkgname="${_pkgname}-roc"
pkgver=1.4.2.5
pkgrel=1
pkgdesc="Open Forward Erasure Codes (ROC fork)"
arch=('x86_64' 'i686' 'aarch64' 'armv7l' 'armv6l' 'armv7h' 'armv6h')
url="https://github.com/roc-streaming/${_pkgname}"
license=('CeCill')
depends=('glibc')
makedepends=('git' 'cmake')
source=("git+https://github.com/roc-streaming/openfec#tag=v${pkgver}")
sha256sums=('SKIP')
provides=("${_pkgname}")
conflicts=("${_pkgname}")

build() {
  cd "$_pkgname"
  rm -rf builddir
  mkdir -p builddir
  cd builddir
  cmake ../ \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib

  make
}

package() {
  cd "$_pkgname/builddir"
  make DESTDIR="${pkgdir}" install

  cd "$srcdir/$_pkgname"
  install -Dm644 pc/openfec.pc "$pkgdir/usr/lib/pkgconfig/openfec.pc"
  install -Dm644 Licence_CeCILL_V2-en.txt "$pkgdir/usr/share/licenses/$_pkgname/LICENSE"

  mkdir -p "$pkgdir/usr/include"
  cp -rfT src "$pkgdir/usr/include/openfec"
  find "$pkgdir/usr/include/openfec" -type d -exec chmod 755 '{}' \;
  find "$pkgdir/usr/include/openfec" -type f -exec chmod 644 '{}' \;
  find "$pkgdir/usr/include/openfec" -type f -not -iname '*.h' -delete
  find "$pkgdir/usr/include/openfec" -type d -empty -delete
}
